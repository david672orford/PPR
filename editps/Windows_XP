#! /usr/bin/perl -w
#
# mouse:~ppr/src/editps/Windows_XP
# Copyright 1995--2005, Trinity College Computing Center.
# Written by David Chappell.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# * Redistributions of source code must retain the above copyright notice,
# this list of conditions and the following disclaimer.
# 
# * Redistributions in binary form must reproduce the above copyright
# notice, this list of conditions and the following disclaimer in the
# documentation and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDERS OR CONTRIBUTORS BE 
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR 
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF 
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS 
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN 
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) 
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE 
# POSSIBILITY OF SUCH DAMAGE.
#
# Last modified 17 June 2005.
#

#
# This filter is for Microsoft Windows XP's PostScript driver (PScript5.dll
# version 5.2*).
#

my $line;

# Copy down to the end of the document setup section but don't print
# the end tag because it is followed by some lines which should be inside
# of it.
while($line = <STDIN>)
	{
	$line =~ s/^(%%Creator: [^\r]+)\r?$/$1, fixed by PPR's Editps/;
	$line =~ s/^(%%IncludeResource:) ProcSet /$1 procset /;
	last if($line =~ /^%%EndSetup\r?$/);
	print $line;
	}

# Move stuff between "%%EndSetup" and start of first page
# into the document setup section.
while($line = <STDIN>)
	{
	last if($line =~ /^%%/);
	print $line;
	}

my $page = 0;
while(defined $line)
	{
	if($line =~ /^%%Page:/)
		{
		my $page_header = $line;
		while($line = <STDIN>)
			{
			# -90 1 581 829 2 1 definenup
			if($line =~ /^[-0-9]+ \d+ \d+ \d+ (\d+) (\d+) definenup\s*$/ && $page == 0)
				{
				my $n = $1 * $2;
				print "%%BeginNonPPDFeature: N-Up $n\n";
				print "%%EndNonPPDFeature\n";
				}
			$page_header .= $line;
			last if($line =~ /^%%EndPageSetup\r?$/);
			}
		print "%%EndSetup\n\n" if($page eq 0);	
		print $page_header;
		$page++;
		}

	# There are reports that Windows XP sometimes omits the "%%Page:" comments
	# and all other comments up to "%%EndPageComments".  So, if we see 
	# "%%EndPageComments" but haven't seen a cooresponding "%%Page:" comment
	# yet, we insert one.
	elsif($line eq "%%EndPageComments\r\n")
		{
		print "%%EndSetup\n\n" if($page == 0);	
		$page++;
		print "%%Page: $page $page\n";
		print $line;
		}

	else
		{
		print $line;
		}

	$line = <STDIN>;
	}

exit 0;
